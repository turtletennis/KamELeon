extends Node

@export var initial_state : PlayerState

var current_state : PlayerState
var states : Dictionary = {}


func _ready():
	for child in get_children():
		if child is PlayerState:
			states[child.name.to_lower()] = child
			child.transitioned.connect(on_child_transition)
	
	if initial_state:
		current_state = initial_state
		current_state.enter.call_deferred()


func _process(delta):
	if current_state:
		current_state.process_state(delta)


func _physics_process(delta):
	if current_state:
		current_state.physics_process_state(delta)

func on_child_transition(state: PlayerState, new_state_name: String):
	if state != current_state:
		return
	
	var new_state = states.get(new_state_name.to_lower())
	if !new_state:
		return
	

	# Clean up the previous state
	if current_state:
		current_state.exit()
		#current_state.process_mode = Node.PROCESS_MODE_DISABLED
	
	# Intialize the new state
	new_state.enter()
	current_state = new_state
